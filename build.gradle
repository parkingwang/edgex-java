buildscript {
    repositories {
        jcenter()
        maven { url "https://maven.aliyun.com/repository/public" }
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.1"
}

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://maven.aliyun.com/repository/jcenter" }
    maven { url "https://dl.bintray.com/nextabc/maven" }
    maven { url "https://dl.bintray.com/yoojia/maven" }
    jcenter()
}


group 'net.nextabc'
version '1.8.0-a1'
description = 'Edge X for java'

sourceCompatibility = 1.8

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    // TOML
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    // MQTT
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1'
    // Kit
    compile 'net.nextabc:toolkit:1.0.0'
    // JSON
    compile 'com.google.code.gson:gson:2.8.5'

    // Lombok, 注意，需要IDE插件支持
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    // Test
    testCompile 'junit:junit:4.12'
}

ext {
    projectGroupId = project.group
    projectVersion = project.version
    projectName = rootProject.name
    projectDesc = description
    projectURL = "https://github.com/nextabc-lab/edgex-java"
    projectVCS = "https://github.com/nextabc-lab/edgex-java.git"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
        title projectName
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    configurations = ['archives']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = projectName
        userOrg = 'nextabc'

        licenses = ['Apache-2.0']
        vcsUrl = projectVCS
        labels = ['Java', 'Mqtt', 'Edge']

        publicDownloadNumbers = true

        version {
            name = projectVersion
            desc = projectDesc
            vcsTag = projectVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

def pomConfig = {

    name projectName
    url projectURL
    inceptionYear '2019'

    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "yoojia"
            name "yoojia chen"
            email "yoojiachen@gmail.com"
        }
    }

    scm {
        url projectVCS
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId projectGroupId
            artifactId projectName
            version projectVersion

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', projectDesc)
                root.children().last() + pomConfig
            }
        }
    }
}
